
plugins {
  id "org.ajoberstar.github-pages" version "1.1.0"
  id "io.silksmith.plugin" version "0.5.0"
}

apply plugin: 'silksmith'

apply plugin: 'idea'
apply plugin: 'org.ajoberstar.github-pages'

repositories {
    jcenter()
    maven { url="http://dl.bintray.com/silksmith-io/silk"}
}
dependencies {
    web "io.silksmith.libs:closure-base:1.0.0+smith.+"
    web "io.silksmith.libs:bootstrap-sass:3.3.3+smith.+"
    web "io.silksmith.libs:jquery:1.11.2+smith.+"

    testWeb "io.silksmith.libs:chai:1.10.0+smith.0"
    testWeb "io.silksmith.libs:sinon:1.12.1+smith.0"
    testWeb "io.silksmith.libs:sinon-chai:2.6.0+smith.0"
}

closureCompileJS{
    entryPoint="silksmith.page"

    // prints compile version and exits
    //args << "--version"

    // changes compilation level (default is ADVANCED_OPTIMIZATIONS)
    args += [
            "--compilation_level",
            "ADVANCED_OPTIMIZATIONS"
    ]

    // sets options on the CompilerOptions directly
    options {
        //useNewTypeInference = true
        languageIn = "ECMASCRIPT6_STRICT"
    }
}
server {
    dir file("www")
}


task assembleStaticFiles(type: Copy) {
    inputs.file "www"
    outputs.dir "$project.buildDir/www"
    from "www"
    into "$project.buildDir/www"
}

githubPages {
    repoUri = 'git@github.com:silksmith/silksmith.github.io.git'
    deleteExistingFiles = true
    targetBranch = "master"
    pages {
        from assembleJS.outputs.files
        from assembleCSS.outputs.files
        from assembleStaticFiles.outputs.files
    }
}

// sets up dependencies for main tasks run by Travis CI
// see: http://docs.travis-ci.com/user/languages/java/#Projects-Using-Gradle
assemble.dependsOn assembleJS, assembleCSS, assembleStaticFiles
check.dependsOn testJS

task wrapper(type: Wrapper) { gradleVersion = '2.3' }

if (project.plugins.hasPlugin("silksmith")) {
    project.idea {
        module {
            if (!scopes.COMPILE) {
                scopes.COMPILE = ["plus": []]
            }
            scopes.COMPILE.plus += [project.configurations.web]

            if (!scopes.TEST) {
                scopes.TEST = ["plus": [], "minus": []]
            }
            scopes.TEST.plus += [project.configurations.testWeb]
            scopes.TEST.minus += [project.configurations.web]
        }
    }
}
